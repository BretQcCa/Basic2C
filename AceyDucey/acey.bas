10 PRINT TAB(26); "ACEY DUCEY-JEU DE CARTE": PRINT: PRINT
20 PRINT TAB(15); "CREATIVE COMPUTING  MORRISTOWN, NEW JERSEY"
21 PRINT
22 PRINT
30 PRINT TAB(15); "ACEY DUCEY SE JOUE COMME CECI: ": PRINT
40 PRINT TAB(15); "LE TENANCIER (ORDINATEUR) RETOURNE 2 CARTES."
50 PRINT TAB(15); "VOUS POUVEZ PARIER OU NE PAS PARIER SELON QUE"
60 PRINT TAB(15); "VOUS PENSEZ QUE LA CARTE SUIVANTE AURA OU N'AURA PAS"
70 PRINT TAB(15); "UNE VALEUR COMPRISE ENTRE CELLES DES 2 AUTRES.": PRINT 
80 PRINT TAB(15); "SI VOUS NE VOULEZ PAS PARIER, ENTREZ 0"
90 PRINT: PRINT
100 N = 100
110 Q = 100
120 PRINT "VOUS AVEZ MAINTENANT"; Q; "DOLLARS."
130 PRINT
140 GOTO 260
210 Q = Q + M
220 GOTO 120
240 Q = Q - M 
250 GOTO 120
260 PRINT "VOICI VOS DEUX CARTES: "
270 A = INT(14 * RND(1)) + 2
280 IF A < 2 THEN 270
290 IF A > 14 THEN 270
300 B = INT(14 * RND(1)) + 2
310 IF B < 2 THEN 300
320 IF B > 14 THEN 300
330 IF A >= B THEN 270
350 IF A < 11 THEN 400
360 IF A = 11 THEN 420
370 IF A = 12 THEN 440
380 IF A = 13 THEN 460
390 IF A = 14 THEN 480
400 PRINT A
410 GOTO 500
420 PRINT "VALET"
430 GOTO 500
440 PRINT "DAME"
450 GOTO 500
460 PRINT "ROI"
470 GOTO 500
480 PRINT "AS"
500 IF B < 11 THEN 550
510 IF B = 11 THEN 570
520 IF B = 12 THEN 590
530 IF B = 13 THEN 610
540 IF B = 14 THEN 630
550 PRINT B
560 GOTO 650
570 PRINT "VALET"
580 GOTO 650
590 PRINT "DAME"
600 GOTO 650
610 PRINT "ROI"
620 GOTO 650
630 PRINT "AS"
640 PRINT
650 PRINT
660 INPUT "QUELLE EST VOTRE MISE?"; M
670 IF M <> 0 THEN 680
675 PRINT "ON SE DEGONFLE!!"
676 PRINT 
677 GOTO 260
680 IF M <= Q THEN 730
690 PRINT "DESOLE, MON POTE, MAIS VOUS Y ALLEZ TROP FORT."
700 PRINT "IL NE VOUS RESTE QUE"; Q; "DOLLARS A MISER."
710 GOTO 650
730 C = INT(14 * RND(1)) + 2
740 IF C < 2 THEN 730
750 IF C > 14 THEN 730
760 IF C < 11 THEN 810
770 IF C = 11 THEN 830
780 IF C = 12 THEN 850
790 IF C = 13 THEN 870
800 IF C = 14 THEN 890
810 PRINT C 
820 GOTO 910
830 PRINT "VALET"
840 GOTO 910
850 PRINT "DAME"
860 GOTO 910
870 PRINT "ROI"
880 GOTO 910
890 PRINT "AS"
900 PRINT
910 IF C > A THEN 930
920 GOTO 970
930 IF C >= B THEN 970
950 PRINT "VOUS AVEZ GAGNE!!!"
960 GOTO 210
970 PRINT "DESOLE, MAIS C'EST PERDU."
980 IF M < Q THEN 240
990 PRINT
1000 PRINT
1010 PRINT "DESOLE, MON POTE, MAIS VOUS ETES RATISSE."
1015 PRINT: PRINT
1020 INPUT "ENCORE UNE FOIS (Y OU N)?"; A$
1025 PRINT: PRINT
1030 IF LEFT$(A$, 1) = "Y" OR LEFT$(A$, 1) = "y" THEN 110
1040 PRINT "O.K., J'ESPERE QUE VOUS AVEZ TROUVE CA AMUSANT!"
1050 END